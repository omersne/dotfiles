#!/bin/zsh

precmd()
{
    local git_branch=
    if [ ! -e ./.no_git_prompt.dfcfg ]; then
        git_branch="$(git branch 2>/dev/null | sed -ne '/^\*/ {s/^\* *//p;}')"
    fi

    if [ -n "$git_branch" ]; then
        git_branch=" %F{226}$git_branch"
        local pwd_color="%F{33}"

        local git_status="$(git status)"

        if grep -q $'\t' <<< "$git_status"; then
            local bracket_color="%F{9}"
        elif ! grep -q "Your branch is up.to.date with " <<< "$git_status"; then
            local bracket_color="%F{226}"
        else
            local bracket_color="%F{33}"
        fi
    else
        local pwd_color="%F{2}"
        local bracket_color="%F{2}"
    fi

    local env_indicator=
    if ___is_ssh_session; then
        env_indicator+="%F{226}S"
    fi
    if ___is_docker; then
        env_indicator+="%F{6}D"
    fi
    if ___is_pipenv; then
        env_indicator+="%F{9}P"
    fi
    if ___is_py_virtualenv; then
        env_indicator+="%F{9}($(_python_virtualenv_project_name))"
    fi

    if [ -n "$env_indicator" ]; then
        env_indicator=" %F{7}{${env_indicator}%F{7}}"
    fi

    if [ "$___NO_RPROMPT" = 1 ]; then
        RPROMPT=""
    else
        RPROMPT="${bracket_color}[${pwd_color}%~${git_branch}${bracket_color}] %(?.%F{2}.%F{9})[%?]${env_indicator}%f"
    fi
    PROMPT="%(!.%F{9}.%F{14})%n%F{63}@%m%(?.%F{226}.%F{9})->%f "
}

